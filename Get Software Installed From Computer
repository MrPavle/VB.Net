Imports Microsoft.Win32

Module Module1

    Sub Main(args() As String)

        Dim ProgramName As String = "GetSoftwareInstalled.exe"

        If args.Length = 0 Then
            Console.WriteLine("")
            Console.WriteLine("Single Computer:")
            Console.WriteLine("  Usage: " & ProgramName & " <ComputerName>")
            Console.WriteLine("")
            Console.WriteLine("Multiple Computers:")
            Console.WriteLine("  Usage: " & ProgramName & " <ComputerName> <ComputerName> <ComputerName>")
            Console.WriteLine("")
            Environment.Exit(0)
        End If

        For Each ComputerName As String In args

            Dim uninstallKey As String = "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall"

            Try
                Using rk As RegistryKey = RegistryKey.OpenRemoteBaseKey(RegistryHive.LocalMachine, ComputerName, RegistryView.Registry64).OpenSubKey(uninstallKey)

                    Console.WriteLine(ComputerName)
                    Console.WriteLine("-------------------------------------------")

                    Dim Applications As New List(Of String)()

                    For Each skName As String In rk.GetSubKeyNames()
                        Using sk As RegistryKey = rk.OpenSubKey(skName)

                            If Not CStr(sk.GetValue("DisplayName")) = "" Then
                                Try
                                    Applications.Add(CStr(sk.GetValue("DisplayName")) & " " & CStr(sk.GetValue("DisplayVersion")))
                                Catch ex As Exception
                                    Console.WriteLine("!!!!!! error: " & ex.Message)
                                End Try
                            End If

                        End Using
                    Next

                    Applications.Sort()
                    For Each app As String In Applications
                        Console.WriteLine(app)
                    Next

                End Using

            Catch ex As Exception
                Console.WriteLine("!! Unable to connect to the computer: " & ComputerName & " !!")
                Console.WriteLine("Error Message: " & ex.Message)
            End Try

        Next ComputerName

    End Sub

End Module
